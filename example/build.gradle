import dependencies.Depends
import dependencies.Versions

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: "$rootDir/example/app-version.gradle"

android {
    compileSdkVersion Versions.androidCompileSdkVersion
    dataBinding.enabled = true
    defaultConfig {
        applicationId "com.coubic.browserexample"
        minSdkVersion Versions.androidMinSdkVersion
        targetSdkVersion Versions.androidTargetSdkVersion
        versionCode project.appVersionCode
        versionName project.appVersionName
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
            ext.alwaysUpdateBuildId = false
        }
        release {
            signingConfig signingConfigs.debug
            shrinkResources true
            minifyEnabled true
        }
    }

    // For faster dev build
    aaptOptions.cruncherEnabled = false

    // For CI
    dexOptions {
        if (System.getenv("CI") as boolean) {
            javaMaxHeapSize "2400M"
            maxProcessCount 1
            threadCount 1
            preDexLibraries false
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            if (System.getenv("CI") as boolean) {
                jvmArgs '-Xmx2048m', '-XX:+HeapDumpOnOutOfMemoryError'
            }
        }
    }
}

dependencies {
    implementation project(":browser")
//    implementation "com.github.coubic:android-browser:0.1.3"

    implementation Depends.Kotlin.stdlib

    // Google
    implementation Depends.AndroidX.appCompat
    implementation Depends.AndroidX.design
    implementation Depends.AndroidX.constraint

    // Test
    testImplementation Depends.Test.junit
    androidTestImplementation Depends.Test.testRunner
}
