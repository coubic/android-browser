import dependencies.Depends
import dependencies.Versions

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

apply from: "$rootDir/browser/app-version.gradle"

android {
    compileSdkVersion Versions.androidCompileSdkVersion
    dataBinding.enabled = true
    defaultConfig {
        minSdkVersion Versions.androidMinSdkVersion
        targetSdkVersion Versions.androidTargetSdkVersion
        versionCode project.appVersionCode
        versionName project.appVersionName
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
        }
    }

    // For CI
    dexOptions {
        if (System.getenv("CI") as boolean) {
            javaMaxHeapSize "2400M"
            maxProcessCount 1
            threadCount 1
            preDexLibraries false
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            if (System.getenv("CI") as boolean) {
                jvmArgs '-Xmx2048m', '-XX:+HeapDumpOnOutOfMemoryError'
            }
        }
    }
}

dependencies {
    implementation Depends.Kotlin.stdlib

    // Google
    implementation Depends.AndroidX.appCompat
    implementation Depends.AndroidX.design
    implementation Depends.AndroidX.constraint

    // Test
    testImplementation Depends.Test.junit
    androidTestImplementation Depends.Test.testRunner
}
